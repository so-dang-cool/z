plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.diffplug.spotless' version '5.+'
    id 'pmd'
    id 'info.solidsoft.pitest' version '1.9.11'

    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'

    // For: ./gradlew --console plain jshell
    id 'com.github.mrsarm.jshell.plugin' version '1.2.0'
}

group = 'so.dang.cool'
version = "3.0.2"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn test
}

tasks.named('build') {
    dependsOn 'jacocoTestCoverageVerification', 'noGremlinsAllowed'
}

javadoc {
    options.addBooleanOption('html5', true)

    // Ignore warnings, especially for missing @param/@return, which is overkill.
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named('jacocoTestCoverageVerification') {
    violationRules {
        rule {
            limit {
                // 100% minimum test coverage.
                //
                // My greatest opponent is myself. If I can discover what my weaknesses are,
                // then I will be an even stronger fighter than I already am. -- Piccolo
                minimum = 1.00
            }
        }
    }
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        csv.required = true
        csv.destination file("${buildDir}/reports/jacoco/report.csv")
    }
}

pitest {
    junit5PluginVersion = '1.0.0'
    targetClasses = ['so.dang.cool.*']
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}

tasks.register('noGremlinsAllowed') {
    dependsOn 'pitest'
    doLast {
        def mutations = new XmlParser().parse("${buildDir}/reports/pitest/mutations.xml")
        def survivorCount = mutations.children().grep { it.attribute('status') != 'KILLED' }.size
        if (survivorCount > 0) {
            throw new Exception("$survivorCount gremlins survived! Details: ${buildDir}/reports/pitest/index.html")
        }
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }

    java {
        toggleOffOn('fmt:off', 'fmt:on')

        importOrder()
        removeUnusedImports()
        prettier(['prettier': '2.3.0', 'prettier-plugin-java': '1.1.1']).config(['parser': 'java', 'tabWidth': 4])
    }
}

tasks.register('fmt') {
    dependsOn 'spotlessApply'
}

pmdTest {
    rulesMinimumPriority = 2
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }

    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'z'

            from components.java

            pom {
                name = 'Z'
                description = 'Function combinators'
                url = 'https://github.com/so-dang-cool/z'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/so-dang-cool/z/blob/HEAD/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'booniepepper'
                        name = 'J.R. Hill'
                        email = 'justin@so.dang.cool'
                        url = 'https://so.dang.cool'
                    }
                }

                contributors {
                    contributor {
                        name = 'Michael Ross'
                        url = 'https://github.com/madmikeross'
                        email = 'michael.s.ross@protonmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/so-dang-cool/z'
                    connection = 'scm:git:git://github.com/so-dang-cool/z.git'
                    developerConnection = 'scm:git:git://github.com/so-dang-cool/z.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('repo')
        }
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/so-dang-cool/z'
            credentials {
                username = 'so-dang-cool'
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
            snapshotRepositoryUrl.set(uri('https://s01.oss.sonatype.org/content/repositories/snapshots/'))
        }
    }
}

/* === Publishing ===

To Maven Central:
$ ./gradlew build publishToSonatype closeAndReleaseSonatypeStagingRepository

To GitHub Packages:
$ ./gradlew build publishAllPublicationsToGitHubPackagesRepository

 */
